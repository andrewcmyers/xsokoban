AC_INIT(xsokoban.man)
  
############################################################################
# Create config files
AC_CONFIG_HEADER(config_local.h)

############################################################################
# Check for programs

AC_ARG_WITH(CC,CC="$withval")

AC_PROG_INSTALL
AC_PROG_CC
AC_PROG_CPP

#####################
# Find Xpm if needed

define(AC_VAR_CHECK,
avc_ic='#include "config.h"'
(echo ${avc_ic}; echo $2) > configure.tmp.$$.h
$1=`$CPP configure.tmp.$$.h | egrep -v "^#"`
rm configure.tmp.$$.h
$1=`echo $$1`
# last line strips out extra whitespace
)

define(AC_FEATURE_CHECK,
AC_MSG_CHECKING(whether $3 is turned on);
AC_VAR_CHECK($1,$2)
if test $$1 = 1; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi
)


AC_FEATURE_CHECK(use_xpm, USE_XPM, use of color bitmaps through XPM)
AC_FEATURE_CHECK(www, WWW, WWW scoring)

if test $use_xpm = 1; then
    libxpm=1
    incxpm=1
    AC_CHECK_LIB(Xpm, main, [LIBS="$LIBS -lXpm"],
	[ 
	  libok=no;
	  AC_MSG_CHECKING(other locations for libXpm)
	  for lib in /usr/local/lib xpm*/lib ../xpm*/lib /usr/X11*/lib /usr/local/*/lib /usr/X11*/lib;
	  do
	    if test -r $lib/libXpm.a; then
		AC_MSG_RESULT(Found it in $lib!)
		LIBS="$LIBS -L$lib -lXpm";
		libok=yes;
		break;
	    fi
	  done
	  if test $libok = no; then
	    AC_MSG_WARN(Can't find libXpm.a! You must add it to LIBS in Makefile.)
	  fi
	])
    AC_HEADER_CHECK(xpm.h,,
	[
	  incok=no;
	  for inc in /usr/include/X11 /usr/local/include /usr/local/include/X11* \
		/usr/local/X11*/include /usr/X11*/include /usr/include/X11*\
		xpm*/lib ../xpm*/lib /usr/local/*/include/X11;
	  do
	    if test -r $inc/xpm.h; then
		AC_MSG_RESULT(Found it in $inc!)
		INCS="$INCS -I$inc";
		incok=yes;
		break;
	    fi
	  done
	  if test $incok = no; then
	    AC_MSG_WARN(Can't find xpm.h! You must add its location to INCS in Makefile.)
	  fi
	  ])
fi
rm -f tmp.$$.c tmp2.$$

###################
# Find X

AC_FIND_X
if test -n "$no_x"; then
  AC_MSG_WARN(Cannot find X libraries! Package may not compile)
  x_includes=
  x_libraries=-lX11
else
  if test "x$x_includes" != x &&
     test "x$x_includes" != x/usr/include; then
    INCS="$INCS -I$x_includes"
  fi 
  if test "x$x_libraries" != x &&
     test "x$x_libraries" != x/usr/lib; then
    LIBS="$LIBS -L$x_libraries"
  fi
  LIBS="$LIBS -lX11"
fi

AC_SUBST(LIBS)
AC_SUBST(INCS)

##############
# Check HERE
if test $www = 1; then
    AC_MSG_CHECKING(location of client)
    AC_VAR_CHECK(HERE,HERE)
    AC_MSG_RESULT($HERE)
    if test "$HERE" = '"@somewhere.somedomain"'; then
      AC_MSG_ERROR(You have not defined the variable HERE in config.h! Do it.)
    fi
fi

##############
# Find installation directory

AC_MSG_CHECKING(installation directory)
AC_VAR_CHECK(ROOTDIR,ROOTDIR)
ROOTDIR=`echo $ROOTDIR | sed -e 's/^"//' -e 's/"$//'`
AC_MSG_RESULT($ROOTDIR)
INSTALL_SCREENS=${ROOTDIR}/screens
INSTALL_SCORES=${ROOTDIR}/scores
AC_SUBST(ROOTDIR)

##################
# Solaris bogosities
AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)

#############################
# DEC OSF/1 bogosity

AC_CHECK_LIB(dnet_stub,main)

##############
# Figure out OWNER
AC_MSG_CHECKING(owner of game)
AC_VAR_CHECK(OWNER, OWNER)
echo "OWNER=$OWNER"
OWNER=`echo $OWNER | sed -e 's/^"//' -e 's/"$//'`
AC_TRY_RUN([
#include <stdio.h>
#include <pwd.h>
#include <stdlib.h>

main(int argc, char **argv) {
    struct passwd *p;
    p = getpwnam("$OWNER");
    exit((p == 0) ? 1 : 0);
}], ownerok=1, ownerok="", ownerok=1)
if test -n "$ownerok"; then
    AC_MSG_RESULT("owner is $OWNER")
    AC_SUBST(OWNER)
else
AC_MSG_WARN(\"$OWNER\" seems not to be a username.");
AC_MSG_WARN(Edit \"config.h\" and change OWNER.");
fi

####################################
# Turn on all compiler warning flags

if test -n "$GCC"
then
  WARN=-Wall
else
  if test -f /bin/uname &&
     test `uname` = OSF1 
  then
	WARN=-std1
  else
	WARN=
  fi
fi
AC_SUBST(WARN)

############################################################################
# Check for library functions and C++ prototypes

AC_MSG_CHECKING(function prototypes)
AC_TRY_LINK([#include <stdlib.h>],
[char *s = getpass("prompt");],
AC_DEFINE(GETPASS_PROTO))

AC_TRY_LINK([#include <stdio.h>],
[fprintf(stderr, "foo");],
AC_DEFINE(FPRINTF_PROTO))

AC_TRY_LINK([#include <stdio.h>],
[FILE *f; fclose(f);],
AC_DEFINE(FCLOSE_PROTO))

AC_TRY_LINK([#include <time.h>],
[time_t t; t = time(&t);],
AC_DEFINE(TIME_PROTO))

AC_TRY_LINK([#include <time.h>],
[time_t t; struct tm stm = *localtime(&t);],
AC_DEFINE(LOCALTIME_PROTO))

AC_TRY_LINK([#include <stdlib.h>],
[char *s = mktemp("fooXXXXXX"); ],
AC_DEFINE(MKTEMP_PROTO))

AC_TRY_LINK(
[#include <errno.h>
#include <stdio.h>],
[perror("bleah"); ],
AC_DEFINE(PERROR_PROTO))

AC_TRY_LINK(
[#include <unistd.h>
#include <stdio.h>],
[int x = rename("foo", "bar");],
AC_DEFINE(RENAME_PROTO))

AC_TRY_LINK(
[#include <string.h>],
[char *s = strdup("foo");],
AC_DEFINE(STRDUP_PROTO))

AC_TRY_LINK(
[#include <stdlib.h>
#include <unistd.h>],
[usleep(1);],
AC_DEFINE(HAS_USLEEP)
AC_DEFINE(USLEEP_PROTO))

AC_TRY_LINK(
[#include <stdlib.h>],
[char *x; int l; bzero(x, l);],
AC_DEFINE(BZERO_PROTO))

AC_TRY_LINK(
[#include <langinfo.h>],
[char *jan = nl_langinfo(ABMON_1);],
AC_DEFINE(HAVE_NL_LANGINFO))

AC_TRY_LINK(
[#include <sys/param.h>],
[int x;],
AC_DEFINE(HAVE_SYS_PARAM_H))

AC_MSG_RESULT(yes)

AC_HAVE_HEADERS(limits.h sys/limits.h sys/select.h malloc.h assert.h)


found_htons=""

define(ACHTONS_CHECK,
    AC_MSG_CHECKING(whether htons & ntohs in $1)
    AC_TRY_LINK([
#include <sys/types.h>
#include <$1>
    ],
    [unsigned short x,y; x = htons(y);],
    [AC_DEFINE($2)
     found_htons=yes
     AC_MSG_RESULT(yes)],
    AC_MSG_RESULT(no)))

ACHTONS_CHECK(machine/endian.h, NEED_ENDIAN)
ACHTONS_CHECK(netinet/in.h, NEED_NETINET_IN)
ACHTONS_CHECK(net/nh.h, NEED_NH)
ACHTONS_CHECK(sys/byteorder.h, NEED_BYTEORDER)

if test -z "$found_htons"
then
  AC_MSG_ERROR(no)
fi

############################################################################
# Build configured files

AC_OUTPUT(Makefile)
